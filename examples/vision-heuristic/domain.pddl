(define (domain vision-reward)
    (:requirements :fluents :adl :typing)
    (:types robot - agent
            red blue yellow green - gem
            gem - item
            item agent - object
    )
    (:predicates
        (has ?a - agent ?i - item)
        (offgrid ?i - item)
        (visible ?a - agent ?o - object)
    )
    (:functions
        (xloc ?o - object) (yloc ?o - object) - integer
        (walls)- bit-matrix
    )
        (:action pickup
        :parameters (?a - agent ?i - item)
        :precondition (and (not (has ?a ?i)) 
                        (= (xloc ?a) (xloc ?i)) 
                        (= (yloc ?a) (yloc ?i)))
        :effect (and (has ?a ?i) 
                        (offgrid ?i)
                        (assign (xloc ?i) -10) 
                        (assign (yloc ?i) -10)
                        (not (visible ?a ?i))
                        (forall (?agent - agent) (not (visible ?agent ?i))))
        )
    (:action up
        :parameters (?a - agent)
        :precondition (and (> (yloc ?a) 1)
                        (= (get-index walls (- (yloc ?a) 1) (xloc ?a)) false))
        :effect (and (decrease (yloc ?a) 1)
                    (forall (?o - object) ; 1-row in front
                        (when (or
                                (and (= (xloc ?o) (xloc ?a))
                                        (= (yloc ?o) (- (yloc ?a) 2)))
                                (and (= (xloc ?o) (- (xloc ?a) 1))
                                        (= (yloc ?o) (- (yloc ?a) 2))
                                        (not (and (= (get-index walls (- (yloc ?a) 1) (- (xloc ?a) 1)) true)
                                                (= (get-index walls (- (yloc ?a) 2) (xloc ?a)) true))))
                                (and (= (xloc ?o) (+ (xloc ?a) 1))
                                        (= (yloc ?o) (- (yloc ?a) 2))
                                        (not (and (= (get-index walls (- (yloc ?a) 1) (+ (xloc ?a) 1)) true)
                                                (= (get-index walls (- (yloc ?a) 2) (xloc ?a)) true)))))
                            (visible ?a ?o)))
                        (forall (?o - item) ; 2-row in front
                                (when (or
                                        (and (= (xloc ?o) (xloc ?a))
                                                (= (yloc ?o) (- (yloc ?a) 3))
                                                (and (= (get-index walls (- (yloc ?a) 2) (xloc ?a)) false)))
                                        (and (= (xloc ?o) (- (xloc ?a) 1))
                                                (= (yloc ?o) (- (yloc ?a) 3))
                                                (and (= (get-index walls (- (yloc ?a) 2) (- (xloc ?a) 1)) false)
                                                    (= (get-index walls (- (yloc ?a) 2) (xloc ?a)) false)))
                                        (and (= (xloc ?o) (- (xloc ?a) 2))
                                                (= (yloc ?o) (- (yloc ?a) 3))
                                                (= (get-index walls (- (yloc ?a) 2) (- (xloc ?a) 1)) false)
                                                (not (and (= (get-index walls (- (yloc ?a) 1) (- (xloc ?a) 1)) true)
                                                        (= (get-index walls (- (yloc ?a) 2) (xloc ?a)) true)))
                                                (not (and (= (get-index walls (- (yloc ?a) 2) (- (xloc ?a) 2)) true)
                                                        (= (get-index walls (- (yloc ?a) 3) (- (xloc ?a) 1)) true))))
                                        (and (= (xloc ?o) (+ (xloc ?a) 1))
                                                (= (yloc ?o) (- (yloc ?a) 3))
                                                (and (= (get-index walls (- (yloc ?a) 2) (xloc ?a)) false)
                                                    (= (get-index walls (- (yloc ?a) 2) (+ (xloc ?a) 1)) false)))
                                        (and (= (xloc ?o) (+ (xloc ?a) 2))
                                                (= (yloc ?o) (- (yloc ?a) 3))
                                                (= (get-index walls (- (yloc ?a) 2) (+ (xloc ?a) 1)) false)
                                                (not (and (= (get-index walls (- (yloc ?a) 1) (+ (xloc ?a) 1)) true)
                                                        (= (get-index walls (- (yloc ?a) 2) (xloc ?a)) true)))
                                                (not (and (= (get-index walls (- (yloc ?a) 2) (+ (xloc ?a) 2)) true)
                                                        (= (get-index walls (- (yloc ?a) 3) (+ (xloc ?a) 1)) true)))))
                            (visible ?a ?o)))))

    (:action down
        :parameters (?a - agent)
        :precondition (and (< (yloc ?a) (height walls))
                        (= (get-index walls (+ (yloc ?a) 1) (xloc ?a)) false))
        :effect (and
                    (increase (yloc ?a) 1)
                    (forall (?o - object) ; 1-row in front
                        (when (or
                                (and (= (xloc ?o) (xloc ?a))
                                        (= (yloc ?o) (+ (yloc ?a) 2)))
                                (and (= (xloc ?o) (- (xloc ?a) 1))
                                        (= (yloc ?o) (+ (yloc ?a) 2))
                                        (not (and (= (get-index walls (+ (yloc ?a) 1) (- (xloc ?a) 1)) true)
                                                (= (get-index walls (+ (yloc ?a) 2) (xloc ?a)) true))))
                                (and (= (xloc ?o) (+ (xloc ?a) 1))
                                        (= (yloc ?o) (+ (yloc ?a) 2))
                                        (not (and (= (get-index walls (+ (yloc ?a) 1) (+ (xloc ?a) 1)) true)
                                                (= (get-index walls (+ (yloc ?a) 2) (xloc ?a)) true)))))
                            (visible ?a ?o)))
                        (forall (?o - object) ; 2-row in front
                                (when (or
                                        (and (= (xloc ?o) (xloc ?a))
                                                (= (yloc ?o) (+ (yloc ?a) 3))
                                                (= (get-index walls (+ (yloc ?a) 2) (xloc ?a)) false))
                                        (and (= (xloc ?o) (- (xloc ?a) 1))
                                                (= (yloc ?o) (+ (yloc ?a) 3))
                                                (and (= (get-index walls (+ (yloc ?a) 2) (- (xloc ?a) 1)) false)
                                                        (= (get-index walls (+ (yloc ?a) 2) (xloc ?a)) false)))
                                        (and (= (xloc ?o) (- (xloc ?a) 2))
                                                (= (yloc ?o) (+ (yloc ?a) 3))
                                                (= (get-index walls (+ (yloc ?a) 2) (- (xloc ?a) 1)) false)
                                                (not (and (= (get-index walls (+ (yloc ?a) 1) (- (xloc ?a) 1)) true)
                                                        (= (get-index walls (+ (yloc ?a) 2) (xloc ?a)) true)))
                                                (not (and (= (get-index walls (+ (yloc ?a) 2) (- (xloc ?a) 2)) true)
                                                        (= (get-index walls (+ (yloc ?a) 3) (- (xloc ?a) 1)) true))))
                                        (and (= (xloc ?o) (+ (xloc ?a) 1))
                                                (= (yloc ?o) (+ (yloc ?a) 3))
                                                (and (= (get-index walls (+ (yloc ?a) 2) (xloc ?a)) false))
                                                        (= (get-index walls (+ (yloc ?a) 2) (+ (xloc ?a) 1)) false))
                                        (and (= (xloc ?o) (+ (xloc ?a) 2))
                                                (= (yloc ?o) (+ (yloc ?a) 3))
                                                (= (get-index walls (+ (yloc ?a) 2) (+ (xloc ?a) 1)) false)
                                                (not (and (= (get-index walls (+ (yloc ?a) 1) (+ (xloc ?a) 1)) true)
                                                        (= (get-index walls (+ (yloc ?a) 2) (xloc ?a)) true)))
                                                (not (and (= (get-index walls (+ (yloc ?a) 2) (+ (xloc ?a) 2)) true)
                                                        (= (get-index walls (+ (yloc ?a) 3) (+ (xloc ?a) 1)) true)))))
                                (visible ?a ?o)))))

    (:action left
        :parameters (?a - agent)
        :precondition (and (> (xloc ?a) 1)
                        (= (get-index walls (yloc ?a) (- (xloc ?a) 1)) false))
        :effect (and (decrease (xloc ?a) 1)
                    (forall (?o - item) ; 1-row in front
                        (when (or
                                (and (= (xloc ?o) (- (xloc ?a) 2))
                                        (= (yloc ?o) (yloc ?a)))
                                (and (= (xloc ?o) (- (xloc ?a) 2))
                                        (= (yloc ?o) (- (yloc ?a) 1))
                                        (not (and (= (get-index walls (- (yloc ?a) 1) (- (xloc ?a) 1)) true)
                                                (= (get-index walls (yloc ?a) (- (xloc ?a) 2)) true))))
                                (and (= (xloc ?o) (- (xloc ?a) 2))
                                        (= (yloc ?o) (+ (yloc ?a) 1))
                                        (not (and (= (get-index walls (+ (yloc ?a) 1) (- (xloc ?a) 1)) true)
                                                (= (get-index walls (yloc ?a) (- (xloc ?a) 2)) true)))))
                            (visible ?a ?o)))
                        (forall (?o - item) ; 2-row in front
                                (when (or
                                        (and (= (xloc ?o) (- (xloc ?a) 3))
                                                (= (yloc ?o) (yloc ?a))
                                                (= (get-index walls (yloc ?a) (- (xloc ?a) 2)) false))
                                        (and (= (xloc ?o) (- (xloc ?a) 3))
                                                (= (yloc ?o) (- (yloc ?a) 1))
                                                (and (= (get-index walls (- (yloc ?a) 1) (- (xloc ?a) 2)) false)
                                                    (= (get-index walls (yloc ?a) (- (xloc ?a) 2)) false)))
                                        (and (= (xloc ?o) (- (xloc ?a) 3))
                                                (= (yloc ?o) (- (yloc ?a) 2))
                                                (= (get-index walls (- (yloc ?a) 1) (- (xloc ?a) 2)) false)
                                                (not (and (= (get-index walls (- (yloc ?a) 1) (- (xloc ?a) 1)) true)
                                                        (= (get-index walls (yloc ?a) (- (xloc ?a) 2)) true)))
                                                (not (and (= (get-index walls (- (yloc ?a) 2) (- (xloc ?a) 2)) true)
                                                        (= (get-index walls (- (yloc ?a) 1) (- (xloc ?a) 3)) true))))
                                        (and (= (xloc ?o) (- (xloc ?a) 3))
                                                (= (yloc ?o) (+ (yloc ?a) 1))
                                                (and (= (get-index walls (yloc ?a) (- (xloc ?a) 2)) false)
                                                    (= (get-index walls (+ (yloc ?a) 1) (- (xloc ?a) 2)) false)))
                                        (and (= (xloc ?o) (- (xloc ?a) 3))
                                                (= (yloc ?o) (+ (yloc ?a) 2))
                                                (= (get-index walls (+ (yloc ?a) 1) (- (xloc ?a) 2)) false)
                                                (not (and (= (get-index walls (+ (yloc ?a) 1) (- (xloc ?a) 1)) true)
                                                        (= (get-index walls (yloc ?a) (- (xloc ?a) 2)) true)))
                                                (not (and (= (get-index walls (+ (yloc ?a) 2) (- (xloc ?a) 2)) true)
                                                        (= (get-index walls (+ (yloc ?a) 1) (- (xloc ?a) 3)) true)))))
                            (visible ?a ?o)))))

    (:action right
        :parameters (?a - agent)
        :precondition (and (< (xloc ?a) (width walls))
                           (= (get-index walls (yloc ?a) (+ (xloc ?a) 1)) false))
        :effect (and (increase (xloc ?a) 1)
                    (forall (?o - object) ; 1-row in front
                        (when (or
                                (and (= (xloc ?o) (+ (xloc ?a) 2))
                                        (= (yloc ?o) (yloc ?a)))
                                (and (= (xloc ?o) (+ (xloc ?a) 2))
                                        (= (yloc ?o) (- (yloc ?a) 1))
                                        (not (and (= (get-index walls (- (yloc ?a) 1) (+ (xloc ?a) 1)) true)
                                                (= (get-index walls (yloc ?a) (+ (xloc ?a) 2)) true))))
                                (and (= (xloc ?o) (+ (xloc ?a) 2))
                                        (= (yloc ?o) (+ (yloc ?a) 1))
                                        (not (and (= (get-index walls (+ (yloc ?a) 1) (+ (xloc ?a) 1)) true)
                                                (= (get-index walls (yloc ?a) (+ (xloc ?a) 2)) true)))))
                            (visible ?a ?o)))
                        (forall (?o - object) ; 2-row in front
                                (when (or
                                        (and (= (xloc ?o) (+ (xloc ?a) 3))
                                                (= (yloc ?o) (yloc ?a))
                                                (= (get-index walls (yloc ?a) (+ (xloc ?a) 2)) false))
                                        (and (= (xloc ?o) (+ (xloc ?a) 3))
                                                (= (yloc ?o) (- (yloc ?a) 1))
                                                (and (= (get-index walls (- (yloc ?a) 1) (+ (xloc ?a) 2)) false)
                                                    (= (get-index walls (yloc ?a) (+ (xloc ?a) 2)) false)))
                                        (and (= (xloc ?o) (+ (xloc ?a) 3))
                                                (= (yloc ?o) (- (yloc ?a) 2))
                                                (= (get-index walls (- (yloc ?a) 1) (+ (xloc ?a) 2)) false)
                                                (not (and (= (get-index walls (- (yloc ?a) 1) (+ (xloc ?a) 1)) true)
                                                        (= (get-index walls (yloc ?a) (+ (xloc ?a) 2)) true)))
                                                (not (and (= (get-index walls (- (yloc ?a) 2) (+ (xloc ?a) 2)) true)
                                                        (= (get-index walls (- (yloc ?a) 1) (+ (xloc ?a) 3)) true))))
                                        (and (= (xloc ?o) (+ (xloc ?a) 3))
                                                (= (yloc ?o) (+ (yloc ?a) 1))
                                                (and (= (get-index walls (yloc ?a) (+ (xloc ?a) 2)) false)
                                                    (= (get-index walls (+ (yloc ?a) 1) (+ (xloc ?a) 2)) false)))
                                        (and (= (xloc ?o) (+ (xloc ?a) 3))
                                                (= (yloc ?o) (+ (yloc ?a) 2))
                                                (= (get-index walls (+ (yloc ?a) 1) (+ (xloc ?a) 2)) false)
                                                (not (and (= (get-index walls (+ (yloc ?a) 1) (+ (xloc ?a) 1)) true)
                                                        (= (get-index walls (yloc ?a) (+ (xloc ?a) 2)) true)))
                                                (not (and (= (get-index walls (+ (yloc ?a) 2) (+ (xloc ?a) 2)) true)
                                                        (= (get-index walls (+ (yloc ?a) 1) (+ (xloc ?a) 3)) true)))))
                            (visible ?a ?o)))))
)
